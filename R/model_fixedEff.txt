
model{
  for(j in 1:J){ # sites 
    # Biological model
    N[j] ~ dpois(lambda[j])
    log(lambda[j]) <- a1 + park[park.ind[j]] + a2*ele[j] + a3*dis[j] + a4*parksize[j] +
        a5*outreach[j] + a6*punishment[j] 
    #Detection model
    y[j] ~ dbin(p[j], n[j])  # y[j] is the number of 1's for site j, out of n[j] days.
    p[j] <- 1-(1-r[j])^N[j]
    logit(r[j]) <- b1 + b2*cam_angle[j]
  }
  
  # Priors:
  sd.p ~ dunif(0,20) 
  tau.p <- 1/(sd.p*sd.p)
  for(i in 1:6) {  # loop over parks
    park[i] ~ dnorm(0, tau.p)
  }
  a1 ~ dunif(-10,10)
  a2 ~ dunif(-5,5)
  a3 ~ dunif(-5,5)
  a4 ~ dunif(-5,10)
  a5 ~ dunif(-5,10)
  a6 ~ dunif(-5,50)  # upper limit increased after seeing early results, still a constraint!
  b1 ~ dunif(-10,10)
  b2 ~ dunif(-5,5)
}
